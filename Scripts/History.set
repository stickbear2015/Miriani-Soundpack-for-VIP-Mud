#If {%Defined(NumberOfBufferedLogMessages)=0} {#Var NumberOfBufferedLogMessages 0}
#If {%Defined(HistoryBuffers)=0} {#Var HistoryBuffers {Output}}
#If {%Defined(CurrentHistoryBuffer)=0} {#var CurrentHistoryBuffer {Output}}
#If {%Defined(CurrentHistoryBufferPosition)=0} {#Var CurrentHistoryBufferPosition 1}

#Alias WriteToLogFile {
 #ForAll {@HistoryBuffers} {
  #If {%Length(%i)>0} {
   #If {@{History%{i}LogBufferEntries}>0} {
    #Var HistoryLogTmp1 {%MudName/%Char - %Trim(%Replace(%i,0, )).log};
    #File 2 {@HistoryLogTmp1};
    #If {%Defined(History%{i}LogInitialization)=1} {
     #Write 2 {.};
     #Close 2;
     #DelFile {@HistoryLogTmp1};
     #File 2 {@HistoryLogTmp1};
     #Write 2 {Log file of %Trim(%Replace(%i,0, )) messages:%CRLF};
     #Exec {#UnVar {History%{i}LogInitialization}}
    };
    #Write 2 {@{History%{i}LogBuffer}};
    #Var {History%{i}LogBuffer} {};
    #Var {History%{i}LogBufferEntries} 0;
    #UnVar HistoryLogTmp1;
    #Close 2
   }
  }
 }
} {}

#Alias PushMessage {
 #Var {History%{1}Tmp1} {%Replace(%2,|,@CR@CR)};
 #If {@SuppressDuplicateEntries>0} {
  #If {%IfWord(@{History%{1}Tmp1},@{History%{1}Messages},|)=1} {
   #If {@SuppressDuplicateEntries=2 or %IfWord(@{History%{1}Tmp1},%Word(@{History%{1}Messages},|,%NumWords(@{History%{1}Messages},|)),|)=1} {#Var {History%{1}Tmp1} {}}
  }
 };
 #If {%Length(@{History%{1}Tmp1})>0} {
  #If {%IfWord(%1,@HistoryBuffers,|)=0} {#Var HistoryBuffers {%Sort(@HistoryBuffers|%1,0)}};
  #If {%NumWords(@{History%{1}Messages},|)>=@HistoryBufferSize} {
   #Var {History%{1}Messages} {%DelNItem(@{History%{1}Messages},1)|@{History%{1}Tmp1}}
  } {
   #If {%Length(@{History%{1}Messages})=0} {#Var {History%{1}Messages} {@{History%{1}Tmp1}}} {#AddItem {History%{1}Messages} {@{History%{1}Tmp1}}}
  };
  #If {@LogHistory=1} {
   #Math NumberOfBufferedLogMessages {@NumberOfBufferedLogMessages+1};
   #Exec {
    #If {~%Defined(History%{1}LogBufferEntries)=0} {
     #Var {History%{1}LogInitialization} 1;
     #Var {History%{1}LogBuffer} {};
     #Var {History%{1}LogBufferEntries} 0
    }
   };
   #Math {History%{1}LogBufferEntries} {@{History%{1}LogBufferEntries}+1};
   #Var {History%{1}LogBuffer} {@{History%{1}LogBuffer}%2%CRLF};
   #If {@NumberOfBufferedLogMessages>@HistoryLoggingInterval} {
    #Var NumberOfBufferedLogMessages 0;
    #Alarm 0 {WriteToLogFile}
   }
  }
 };
 #Exec {#UnVar {History%{1}Tmp1}}
} {}

#Alias spc {#Exec {#Alarm 0 {SPHB copy {%0}}}}
#Alias spr {#Exec {#Alarm 0 {SPHB read {%0}}}}
#Alias sps {#Exec {#Alarm 0 {SPHB switch {%0}}}}
#Alias sph {#Exec {#Alarm 0 {SPHistory %0}}}
#Alias SPHistory {
 #Say {SPHistory aliases:
  SPHB: History Backend.
  spr: Read history buffer, same as SPHB read.
  spc: Copy history buffer, same as SPHB copy.
  sps: Switch history buffer, same as SPHB switch.}
}
#Alias SPHB {
 Speech Stop;
 #If {%Length(%1)=0} {
  #Say {Valid arguments are back, forward, copy, read, and switch.};
  #Abort All
 };
 #Var HistoryTmp1 {%Trim(%1)*};
 #If {"read"=*@HistoryTmp1 or "copy"=*@HistoryTmp1} {
  #If {%Length(%2)>0} {
   #Var HistoryTmp2 {%Trim(%2)*};
   #ForAll {@HistoryBuffers} {
    #If {%i=*@HistoryTmp2} {
     #Var HistoryTmp2 {%i}
    }
   };
   #If {%Pos(*,@HistoryTmp2)=0} {
    #Exec {
     #If {~@HistoryTmp2<>"Output" and ~%Defined(History@{HistoryTmp2}Messages)=0} {
      #Say {The @HistoryTmp2 buffer is empty} NoDisplay;
      #Abort All
     }
    };
    #If {%IsNumber(%3)=1} {
     #If {@HistoryTmp2="Output"} {
      #Var HistoryTmp3 10
     } {
      #Var HistoryTmp3 {%NumWords(@{History@{HistoryTmp2}Messages},|)}
     };
     #If {(%IsNumber(%4)=1 and %3<>%4) or (%4="+" and %3<@HistoryTmp3) or (%4="-" and %3>1)} {
      #If {%IsNumber(%4)=1} {
       #Var HistoryTmp4 {%4}
      } {
       #If {%4="-"} {
        #Var HistoryTmp4 {1}
       } {
        #Var HistoryTmp4 {@HistoryTmp3}
       }
      };
      #If {%Min(%3,@HistoryTmp4)<1 or %Max(%3,@HistoryTmp4)>@HistoryTmp3} {
       #Say {Valid range for the %Replace(@HistoryTmp2,0, ) buffer is 1 to @HistoryTmp3} NoDisplay;
       #Abort All
      };
      #Var HistoryTmp5 {};
      #If {@HistoryTmp2="Output"} {
       #Var HistoryTmp6 {%Max(%3,@HistoryTmp4)};
       #While {@HistoryTmp6>=%Min(%3,@HistoryTmp4)} {
        #Var HistoryTmp5 {@HistoryTmp5%CRLF%LastLine(@HistoryTmp6)};
        #Math HistoryTmp6 {@HistoryTmp6-1}
       }
      } {
       #Math HistoryTmp6 {@HistoryTmp3+1-%Max(%3,@HistoryTmp4)};
       #Math HistoryTmp7 {@HistoryTmp3+1-%Min(%3,@HistoryTmp4)};
       #While {@HistoryTmp6<=@HistoryTmp7} {
        #Var HistoryTmp5 {@HistoryTmp5%CRLF%Word(@{History@{HistoryTmp2}Messages},|,@HistoryTmp6)};
        #Math HistoryTmp6 {@HistoryTmp6+1}
       };
       #Var HistoryTmp5 {%Replace(@HistoryTmp5,@CR@CR,|)}
      };
      #If {"read"=*@HistoryTmp1} {
       #Say {Message %Min(%3,@HistoryTmp4) to %Max(%3,@HistoryTmp4) in the %Replace(@HistoryTmp2,0, ) buffer:@HistoryTmp5}
      } {
       #Clipboard {%Copy(@HistoryTmp5,3)};
       #Say {Copied message %Min(%3,@HistoryTmp4) to %Max(%3,@HistoryTmp4) from the %Replace(@HistoryTmp2,0, ) buffer} NoDisplay
      }
     } {
      #If {@HistoryTmp2="Output"} {
       #If {%3<1 or %3>10} {
        #Var HistoryTmp1 {read};
        #Var HistoryTmp3 {Valid range for the Output buffer is 1 to 10}
       } {
        #Var HistoryTmp3 {%LastLine(%3)}
       }
      } {
       #Exec {
        #If {~%Defined(History@{HistoryTmp2}Messages)=1} {
         #If {%3<1 or %3>%NumWords(@{History@{HistoryTmp2}Messages},|)} {
          #Var HistoryTmp1 {read};
          #Var HistoryTmp3 {That message does not exist. Valid range for the %Replace(@HistoryTmp2,0, ) buffer is currently 1 to %NumWords(@{History@{HistoryTmp2}Messages},|)}
         } {
          #Var HistoryTmp3 {~%Word(~@{History@{HistoryTmp2}Messages},|,%Eval(%NumWords(@{History@{HistoryTmp2}Messages},|)+1-%3))};
          #Var HistoryTmp3 {~%Replace(~@HistoryTmp3,~@CR~@CR,|)}
         }
        } {
         #Var HistoryTmp1 {read};
         #Var HistoryTmp3 {No @HistoryTmp2 messages yet}
        }
       }
      };
      #If {"read"=*@HistoryTmp1} {
       #Say {@HistoryTmp3} NoDisplay
      } {
       #Clipboard {@HistoryTmp3};
       #Say {Copied message: @HistoryTmp3} NoDisplay
      }
     }
    } {
     #If {@HistoryTmp2="Output"} {
      #Var HistoryTmp3 {};
      #If {"read"=*@HistoryTmp1} {
       #10 {#Var HistoryTmp3 {@HistoryTmp3%CRLF %LastLine(11-%i)}}
      } {
       #10 {#Var HistoryTmp3 {@HistoryTmp3%CRLF%LastLine(%i)}}
      }
     } {
      #Exec {
       #If {~%Defined(History@{HistoryTmp2}Messages)=1} {
        #Var HistoryTmp3 {~%Replace(~@{History@{HistoryTmp2}Messages},|,~%CRLF )};
        #Var HistoryTmp3 {%CRLF ~%Replace(~@HistoryTmp3,~@CR~@CR,|)}
       } {
        #Say {The @HistoryTmp2 buffer is empty} NoDisplay;
        #Abort All
       }
      }
     };
     #If {"read"=*@HistoryTmp1} {
      #Say {All content of the %Replace(@HistoryTmp2,0, ) buffer:@HistoryTmp3}
     } {
      #Clipboard {%Copy(@HistoryTmp3,3)};
      #Say {Copied the %Replace(@HistoryTmp2,0, ) buffer} NoDisplay
     }
    }
   } {
    #Say {That buffer does not exist} NoDisplay
   }
  } {
   #If {"read"=*@HistoryTmp1} {
    #Say {History buffers:%CRLF %Replace(%Replace(~@HistoryBuffers,0, ),|,~, )}
   } {
    #Var HistoryTmp3 {%Replace(%Replace(~@HistoryBuffers,0, ),|,~, )};
    #Clipboard {@HistoryTmp3};
    #Say {Copied list of history buffers} NoDisplay
   }
  };
  #Abort All
 };
 #If {"switch"=*@HistoryTmp1} {
  #If {%2="back" or %2="forward"} {
   #Var HistoryTmp1 0;
   #Var HistoryTmp2 {%NumWords(@HistoryBuffers,|)};
   #While {@HistoryTmp1<@HistoryTmp2} {
    #Math HistoryTmp1 {@HistoryTmp1+1};
    #If {@CurrentHistoryBuffer=%Word(@HistoryBuffers,|,@HistoryTmp1)} {
     #If {%2="back"} {
      #If {@HistoryTmp1>1} {
       #Math HistoryTmp1 {@HistoryTmp1-1};
       #Var CurrentHistoryBuffer {%Word(@HistoryBuffers,|,@HistoryTmp1)}
      } {
       #Var CurrentHistoryBuffer {%Word(@HistoryBuffers,|,@HistoryTmp2)}
      }
     } {
      #If {@HistoryTmp1=@HistoryTmp2} {
       #Var CurrentHistoryBuffer {%Word(@HistoryBuffers,|,1)}
      } {
       #Math HistoryTmp1 {@HistoryTmp1+1};
       #Var CurrentHistoryBuffer {%Word(@HistoryBuffers,|,@HistoryTmp1)}
      }
     };
     #Var HistoryTmp1 {@HistoryTmp2}
    }
   }
  } {
   #Var HistoryTmp1 {%Trim(%2)*};
   #ForAll {@HistoryBuffers} {
    #If {%i=*@HistoryTmp1} {
     #Var HistoryTmp1 {%i}
    }
   };
   #If {%Pos(*,@HistoryTmp1)=0} {
    #Var CurrentHistoryBuffer {@HistoryTmp1}
   } {
    #Say {That buffer does not exist} NoDisplay;
    #Abort All
   }
  };
  #Say {%Replace(@CurrentHistoryBuffer,0, )} VoiceOnly;
  #Var CurrentHistoryBufferPosition 1;
  #Abort All
 };
 #Exec {
  #If {~@CurrentHistoryBuffer<>"Output" and ~%Defined(History@{CurrentHistoryBuffer}Messages)=0} {
   #Say {No messages in the @CurrentHistoryBuffer buffer yet} NoDisplay;
   #Abort All
  }
 };
 #If {"back"=*@HistoryTmp1} {
  #If {%2="all"} {
   #If {@CurrentHistoryBuffer="Output" and @HistoryBufferSize>10} {
    #Var CurrentHistoryBufferPosition 10
   } {
    #Var CurrentHistoryBufferPosition {%Min(%NumWords(@{History@{CurrentHistoryBuffer}Messages},|),@HistoryBufferSize)}
   };
   #Var HistoryTmp1 0
  } {
   #Math CurrentHistoryBufferPosition {@CurrentHistoryBufferPosition+1};
   #If {@CurrentHistoryBuffer="Output"} {
    #If {@CurrentHistoryBufferPosition>10} {
     #Var CurrentHistoryBufferPosition 10;
     #Var HistoryTmp1 1
    } {
     #Var HistoryTmp1 0
    }
   } {
    #If {@CurrentHistoryBufferPosition>%NumWords(@{History@{CurrentHistoryBuffer}Messages},|)} {
     #Var CurrentHistoryBufferPosition {%NumWords(@{History@{CurrentHistoryBuffer}Messages},|)};
     #Var HistoryTmp1 1
    } {
     #Var HistoryTmp1 0
    }
   };
   #If {@HistoryTmp1=1 and @Silent=0} {#Play {@SoundsPath\Ship\Computer\Beep@Ext} @Vol}
  };
  #Var HistoryTmp1 {}
 };
 #If {"forward"=*@HistoryTmp1} {
  #Math CurrentHistoryBufferPosition {@CurrentHistoryBufferPosition-1};
  #If {@CurrentHistoryBufferPosition<1 or %2="all"} {
   #If {@CurrentHistoryBufferPosition<1 and @Silent=0} {#Play {@SoundsPath\Ship\Computer\Beep@Ext} @Vol};
   #Var CurrentHistoryBufferPosition 1
  };
  #Var HistoryTmp1 {}
 };
 #If {@HistoryTmp1<>""} {
  #Say {I don't recognize that argument} NoDisplay;
  #Abort All
 };
 #If {@CurrentHistoryBuffer="Output"} {
  #Say {%LastLine(@CurrentHistoryBufferPosition)} NoDisplay;
  #If {@PrintVoiceOnly=1} {#Say {[Review] %LastLine(@CurrentHistoryBufferPosition)} NoVoice}
 } {
  #Var HistoryTmp1 {%Word(@{History@{CurrentHistoryBuffer}Messages},|,%NumWords(@{History@{CurrentHistoryBuffer}Messages},|)+1-@CurrentHistoryBufferPosition)};
  #Var HistoryTmp1 {%Replace(@HistoryTmp1,@CR@CR,|)};
  #If {@CurrentHistoryBuffer="Played0Sounds" and @PlaySoundsOnReview=1 and @Silent=0} {
   #If {%Pos(:,@HistoryTmp1)>0} {
    #Var HistoryTmp2 {%Pos(:,@HistoryTmp1)};
    #Math HistoryTmp2 {@HistoryTmp2+8};
    #Var HistoryTmp3 {%Pos(" at ",@HistoryTmp1)};
    #If {%NumWords(@HistoryTmp1,:)>2} {
     #Var HistoryTmp4 {}
    } {
     #Var HistoryTmp4 {@SoundsPath\}
    };
    #If {%Defined(HistorySoundHandle)=1} {
     #PC @HistorySoundHandle Stop
    };
    #Play {@HistoryTmp4%Copy(@HistoryTmp1,@HistoryTmp2+2,@HistoryTmp3-(@HistoryTmp2+2))} {%Min(@Vol+@HistoryVolumeOffset,100)};
    #Var HistorySoundHandle %PlayHandle;
    #PC @HistorySoundHandle Pan @HistoryPanning
   }
  };
  #Say {@HistoryTmp1} NoDisplay;
  #If {@PrintVoiceOnly=1} {
   #Say {[Review] @HistoryTmp1} NoVoice
  }
 }
} {}

#Alias BufferConfig {
 #If {%1="r" or %1="reset"} {
  #Say {Resetting buffers for review to their default values.};
  #Var BufferConfigTmp1 {BuffersForReview:};
  #Var BuffersForReview {%Word(%Word(@ConfOptionVars,@BufferConfigTmp1,2),%CRLF,1)}
 };
 #If {%NumWords(@BuffersForReview,~,)<>5} {
  #Say {List of buffers is malformed. Resetting to defaults.};
  #Var BufferConfigTmp1 {BuffersForReview:};
  #Var BuffersForReview {%Word(%Word(@ConfOptionVars,@BufferConfigTmp1,2),%CRLF,1)}
 };
 #If {%IsNumber(%1)=1} {
  #If {%1=3} {
   #Say {The following methods are available for changing the current buffer for Control 1-0:};
   #Say {Control E and Control Shift E allow you to browse through the buffers back and forth.};
   #Say {hist sw <buffername> allows you to set the buffer directly. E.g. you could type hist sw rooc to set the buffer to rooc. hist sw is short for history switch, and you can see more history commands by typing hist.};
   #Say {Also note that most of the buffers are not available until they have received some content.};
   #Abort All
  };
  #If {%1=5 and @ExcludeAltGRNumrow=1} {
   #Say {You need to disable the option for excluding AltGR Numrow first, in conf advanced keystrokes.};
   #Abort All
  };
  #If {%1>0 and %1<7} {
   #Var BufferConfigTmp1 {%Replace(@BuffersForReview,~,,|)};
   #Var BufferConfigTmp2 0;
   #If {%1>3} {
    #Math BufferConfigTmp3 {%1-1}
   } {
    #Var BufferConfigTmp3 %1
   };
   #Var SelectedBuffer {};
   #Var BuffersForReview {};
   #ForAll {@BufferConfigTmp1} {
    #Math BufferConfigTmp2 {@BufferConfigTmp2 + 1};
    #If {@BufferConfigTmp3=@BufferConfigTmp2} {
     #Var BufferSelectionTmp2 {%i};
     #If {%Length(%2)>0 and %2<>"SPPrompt"} {
      #Var SelectedBuffer {%2}
     } {
      #Var BufferSelectionTmp1 {P:Select buffer};
      #ForAll {Comm|Computer|Flight|Metafrequency0Channels|Newbie|PA|rooc|Say} {
       #If {%Pos(%i,@HistoryBuffers)=0} {#AddItem HistoryBuffers {%i}}
      };
      #Var HistoryBuffers {%Sort(@HistoryBuffers,0)};
      #ForAll {@HistoryBuffers} {
       #If {@BufferSelectionTmp2=%i} {
        #AddItem BufferSelectionTmp1 {*%Replace(%i,0, ):%i}
       } {
        #AddItem BufferSelectionTmp1 {%Replace(%i,0, ):%i}
       }
      };
      #Var SelectedBuffer {%Pick(@BufferSelectionTmp1)};
      #UnVar BufferSelectionTmp1
     };
     #If {@SelectedBuffer<>""} {
      #Var BuffersForReview {@BuffersForReview,@SelectedBuffer}
     } {
      #Var BuffersForReview {@BuffersForReview,@BufferSelectionTmp2}
     };
     #UnVar BufferSelectionTmp2
    } {
     #Var BuffersForReview {@BuffersForReview,%I}
    }
   };
   #Var BuffersForReview {%Copy(@BuffersForReview,2)};
   #If {@SelectedBuffer<>""} {
    #Say {Set!}
   };
   #UnVar SelectedBuffer;
   #UnVar BufferConfigTmp3
  }
 };
 #If {%Length(%1)=0 or %2="SPPrompt"} {
  #Var BufferConfigTmp2 {%Replace(@BuffersForReview,0, )};
  #Var BufferConfigTmp1 {[1] Alt 1-0: %Word(@BufferConfigTmp2,~,,1).
[2] Alt Shift 1-0: %Word(@BufferConfigTmp2,~,,2).
[3] Control 1-0: Currently set to %Replace(@CurrentHistoryBuffer,0, ), and it will reflect the current buffer set by Control E or Control Shift E, or directly using the command hist sw <buffername>.
[4] Control Shift 1-0: %Word(@BufferConfigTmp2,~,,3).};
  #If {@ExcludeAltGRNumrow=1} {
   #Var BufferConfigTmp1 {@BufferConfigTmp1
[5] Control Alt 1-0: Currently disabled, but you can enable it in conf advanced keystrokes (set Exclude AltGR Numrow to off).}
  } {
   #Var BufferConfigTmp1 {@BufferConfigTmp1
[5] Control Alt 1-0: %Word(@BufferConfigTmp2,~,,4).}
  };
  #Var BufferConfigTmp1 {@BufferConfigTmp1
[6] Control Alt Shift 1-0: %Word(@BufferConfigTmp2,~,,5).
[r] Reset to default.};
  SPPrompt {History buffer configuration} {@BufferConfigTmp1} {BufferConfig} {SPPrompt}
 };
 #UnVar BufferConfigTmp1;
 #UnVar BufferConfigTmp2
} {}

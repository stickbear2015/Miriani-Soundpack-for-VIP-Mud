#Trigger {*--*} {
 #If {%Length(%Trim(%Replace(%0,-,)))=0} {
  #If {@GagDashes=1 and (@Main<-9 or @Main>0)} {#GagLine 1 All};
  #If {@Main>0 and %Defined(Scanning)=1 and @ScanOutputLabelsDetected>0} {
   ScanProcessOutput;
   #If {%Defined(ScanFilterVar)=1} {
    #UnGagLine;
    #If {%Length(@{ScanObject@{ScanFilterVar}Raw})>0} {
     #Var ScanFormatOutput {@{ScanObject@{ScanFilterVar}Raw}}
    } {
     #Var ScanFormatOutput {It has no @ScanFilter.}
    };
    #If {@InterruptOnFilteredScans=1 or (@InterruptOnScanningStarships=1 and @ScanObjectObjectTypeRaw="Starship")} {Speech Stop};
    #Sub {@ScanFormatOutput}
   } {
    #If {@ScanFilters>0} {
     #UnGagLine;
     ScanFormat {@ScanObjectObjectTypeRaw};
     #If {@InterruptOnScanningStarships=1 and @ScanObjectObjectTypeRaw="Starship"} {Speech Stop};
     #Sub {@ScanFormatOutput@ScanFormatExtraOutput}
    }
   }
  };
  #If {@Main>0 and %Defined(DamageReader)=1} {#Class DamageReader Disable;#UnVar DamageReader}
 }
}

#Trigger {(* second* roundtime.)} {
 #If {%IsNumber(%1)=1} {
  #If {@PlayRoundtimeSound=1 or @ShowRoundtimeIsOver=1} {
   #Alarm {%Eval(%1-1)} {
    #If {@ShowRoundtimeIsOver=1} {#Say {Go!}};
    #If {@PlayRoundtimeSound=1} {msplay ship\computer\beep@Ext}
   }
  };
  #If {%1<@RoundTimeThreshold} {#GagLine 1 Voice};
  #Sub {%1 sec%2 roundtime.}
 }
} {Main}
#Trigger {Announcement*: *} {
 msplay General\Misc\Announcement@Ext;
 PushMessage Announcements {%0}
} {Main}
#Trigger {Game Change: *} {
 #Sub {[Game Change]: %1};
 msplay General\Misc\GameChange*2@Ext;
 PushMessage Changelog %0
} {Main}
#Trigger {That exit appears to be blocked.} {msplay General\Misc\CantGo*5@Ext} {Main}
#Trigger {* crumple* up and throw* it away.} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0} {
  msplay General\Misc\Paper*3@Ext
 }
} {Main}
#Trigger {* attempts to board *, but is turned away by *.} {msplay General\Misc\CantGo*5@Ext} {Main}
#Trigger {* suddenly steps from the background.} {
 msplay General\Misc\HostIn*3@Ext;
 #Sub {%1 appears.}
} {Main}
#Trigger {* coalesces into being nearby.} {
 msplay General\Misc\HostIn*3@Ext;
 #Sub {%1 appears.}
} {Main}
#Trigger {* suddenly seems to disappear into the background.} {
 msplay General\Misc\HostOut*3@Ext;
 #Sub {%1 disappears.}
} {Main}
#Trigger {* dissolves into billions of constituent particles and disperses into a hitherto unnoticed breeze.} {
 msplay General\Misc\HostOut*3@Ext;
 #Sub {%1 disappears.}
} {Main}
#Trigger {I don't understand that.} {
 #If {@GagDontUnderstandMessages=1} {#GagLine 1 All};
 msplay General\Misc\Command*10@Ext;
 #If {@Main<0} {MainTrigSwitch 1}
}
#Trigger {*[[]Enter "yes" or "no"]} {
 msplay General\Misc\prompt@Ext;
 #Sub {%1[Y/N]}
} {Main}
#Trigger {Your choice:} {msplay General\Misc\prompt@Ext} {Main}
#Trigger {Enter your selection[.:]} {msplay General\Misc\prompt@Ext} {Main}
#Trigger {[[]Type a line of input or [`]~~@abort' to abort the command.]} {
 #If {@Main<0 or %IfWord(%LastLine(1),Enter your selection.|Enter your selection:|Your choice:,|)=0} {
  msplay General\Misc\prompt@Ext;
  #Sub {Type a line of input, use ~@abort to cancel.};
  #GagLine 1 Voice
 } {
  #GagLine 1 All
 };
 #If {@Main<0} {MainTrigSwitch 1}
}
#Trigger {[[]Type lines of input; *]} {
 #If {@ConfPrintQuietly=1} {
  #Var ConfPrintQuietly 0
 } {
  msplay General\Misc\prompt@Ext;
  #GagLine 1 Voice;
  #If {@Main<0} {MainTrigSwitch 1}
 }
}
#Trigger {>> Command Aborted <<} {
 msplay General\Misc\Cancel@Ext;
 #Sub {Aborted.};
 #GagLine 1 Voice;
 #If {@Main<0} {MainTrigSwitch 1}
}
#Trigger {Invalid selection.} {
 msplay General\Misc\Cancel@Ext;
 MainTrigSwitch 1
}

#Trigger {* gestures for you to join * group, which you promptly do.} {
 #Sub {%1 adds you to %2 group.};
 msplay General\Misc\joins@Ext
} {Main}
#Trigger {You gesture towards *, who promptly joins your group.} {
 msplay General\Misc\joins@ext;
 #Sub {You add %1 to your group.}
} {Main}
#Trigger {* indicates that * no longer wishes you to follow *.*} {msplay General\Misc\disband@Ext} {Main}
#Trigger {* leaves your group.*} {msplay General\Misc\disband@Ext} {Main}
#Trigger {* begins following you.*} {msplay General\Misc\joins@Ext} {Main}
#Trigger {* take* control of *'s group.} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0} {
  #If {%Pos(You request,%1)=0} {msplay General\Misc\joins@Ext}
 }
} {Main}

#Trigger {*A small lock destroying explosive suddenly explodes, *.} {msplay Ship\Misc\HatchBomb@Ext} {Main}
#Trigger {*You carefully place * along the edge of * hatch. You press a small button and step back as the charge begins its * second countdown*} {msplay Ship\Misc\HatchBombPlace@Ext} {Main}
#Trigger {*You hear*strange sound* coming from the airlock*} {msplay Ship\Misc\HatchBombPlaceHere@Ext} {Main}
#Trigger {*You hear what sounds like an explosion coming from the general area of the airlock*} {msplay Ship\Misc\HatchBombHere@Ext} {Main}

#Trigger {* hear a deep rumbling as the docking bay door *s.*} {
 #If {%IfWord(%2,open|close,|)=1} {
  msplay Ship\Misc\DockingBayDoor%{2}@Ext
 }
} {Main}
#Trigger {* to the * slides *.} {
 #Var DoorTmp1 {%Word(%3, ,1)};
 #If {%IfWord(@DoorTmp1,open|closed,|)=1} {
  msplay Ship\Misc\Door@{DoorTmp1}Stateroom@Ext
 };
 #UnVar DoorTmp1
} {Main}
#Trigger {* leading * slides *.} {
#Var DoorTmp1 {%Word(%3, ,1)};
#If {@DoorTmp1="open" OR @DoorTmp1="closed"} {
msplay Ship\Misc\Door@{DoorTmp1}Stateroom@Ext
}
} {Main}

#Trigger {Directions from * to *: *.} {
 #Sub {%2: %3.}
} {Main}
#Trigger {* arrives, looking vaguely puzzled.} {msplay General\Misc\DoorChime@Ext} {Main}
#Trigger {* several automated drones begin healing *.} {msplay General\Misc\ITPNStart@Ext} {Main}
#Trigger {* stands up, looking much healthier than before.} {msplay Stand@Ext} {Main}
#Trigger {You follow *.} {
 #Var Following 1;
 #If {%Pos(into the ship,%1)>0} {
  #If {%IfWord(@RoomName,Airlock,@CR)=0} {
   msplay Ship\Misc\AirlockEnter@Ext
  };
  #Var Dir {enter}
 } {
  #If {%Pos(outside the ship,%1)>0} {
   #Var Dir {out};
   ThemeMusicFadeOut;
   msplay Ship\Misc\AirlockExit@Ext
  } {
   #If {%IfWord(%Word(%1, ,%NumWords(%1, )),@DirList, )=1} {
    #Var Dir {%Word(%1, ,%NumWords(%1, ))};
    MStep You Leave {@Dir}
   } {
    #If {%Pos(into the airlock,%1)>0} {
     #Var Dir {out};
     MStep You Leave {@Dir}
    } {
     #If {%Pos(through the,%1)>0} {
      #Var Dir {go};
      MirianiSoundPlay Standard General\Misc\Door@Ext;
      MStep You Leave {@Dir}
     } {
      #Var Dir {}
     }
    }
   }
  }
 };
 #If {@SoundsVersion>4 and @DirAnnouncements=1 and %Length(@Dir)>0} {
  #GagLine 1 Voice;
  MirianiSoundPlay Direction {General\Directions\@Dir@Ext} {@DirAnnouncementVolumeDiff} {} 5
 }
} {Main}
#Trigger {* drags you *.} {
 #If {%Pos(:,%1)=0 and %Pos(~,,%1)=0 and %IfWord(%Left(%1,1),>| |-|#|~(|[,|)=0 and %IfWord(%Word(%2, ,1),off, )=0} {
  #Var Following 1;
  #If {%Pos(into the ship,%2)>0} {
   #If {%IfWord(@RoomName,Airlock,@CR)=0} {
    msplay Ship\Misc\AirlockEnter@Ext
   };
   #Var Dir {enter}
  } {
   #If {%Pos(outside the ship,%2)>0} {
    #Var Dir {out};
    ThemeMusicFadeOut;
    msplay Ship\Misc\AirlockExit@Ext
   } {
    #If {%IfWord(%Word(%2, ,%NumWords(%2, )),@DirList, )=1} {
     #Var Dir {%Word(%2, ,%NumWords(%2, ))};
     MStep You Leave {@Dir}
    } {
     #If {%Pos(into the airlock,%2)>0} {
      #Var Dir {out};
      MStep You Leave {@Dir}
     } {
      #If {%Pos(through the,%2)>0} {
       #Var Dir {go};
       MirianiSoundPlay Standard General\Misc\Door@Ext;
       MStep You Leave {@Dir}
      } {
       #Var Dir {}
      }
     }
    }
   }
  };
  #If {@SoundsVersion>4 and @DirAnnouncements=1 and %Length(@Dir)>0} {
   #GagLine 1 Voice;
   MirianiSoundPlay Direction {General\Directions\@Dir@Ext} {@DirAnnouncementVolumeDiff} {} 5
  }
 }
} {Main}
#Trigger {*.*} {
 #If {%NumWords(%1, )>1 and %Length(%2)=0 and %Pos(:,%0)=0 and %Pos(~,,%0)=0 and %IfWord(%Left(%1,1),>| |.|-|#|~(|[,|)=0} {
  #Var MovementTestTmp2 {%Word(%1, ,%NumWords(%1, ))};
  #Var MovementTestTmp3 {};
  #If {%IfWord(@MovementTestTmp2,@DirList, )=0} {
   #Var MovementTestTmp1 {%Pos( on a ,%1)};
   #If {@MovementTestTmp1=0} {#Var MovementTestTmp1 {%Pos( on an ,%1)}};
   #If {@MovementTestTmp1>0} {
    #Var MovementTestTmp1 {%Copy(%1,1,@MovementTestTmp1-1)};
    #Var MovementTestTmp2 {%Word(@MovementTestTmp1, ,%NumWords(@MovementTestTmp1, ))};
    #If {%IfWord(@MovementTestTmp2,@DirList, )=1} {
     #Var MovementTestTmp3 {%Copy(%1,%Length(@MovementTestTmp1)+5)};
     #Var MovementTestTmp1 {%Copy(%1,1,%Length(@MovementTestTmp1)-%Length(@MovementTestTmp2)-1)}
    } {
     #Var MovementTestTmp2 {}
    }
   } {
    #Var MovementTestTmp2 {}
   }
  } {
   #Var MovementTestTmp1 {%Copy(%1,1,%Length(%1)-%Length(@MovementTestTmp2)-1)}
  };
  #If {%Length(@MovementTestTmp2)>0} {
   #Var MovementTmp1 {@MovementTestTmp1};
   #Var MovementDir {@MovementTestTmp2};
   #Var MovementMount {@MovementTestTmp3};
   #Var MovementTmp4 3;
   #If {%Pos(You ,@MovementTmp1)=1} {
    #If {%Pos(You can ,@MovementTmp1)=0} {
     #Var MovementTmp2 {%NumWords(@MovementTmp1, )};
     #Var MovementTmp3 {%Word(@MovementTmp1, ,@MovementTmp2)};
     #While {@MovementTmp2>1 and @MovementTmp4>0} {
      #Math MovementTmp4 {@MovementTmp4-1};
      #If {%IfWord(@MovementTmp3,@WalkStyles,%CRLF)=1} {
       #Var MovementTmp2 {-1}
      } {
       #Math MovementTmp2 {@MovementTmp2-1};
       #Var MovementTmp3 {%Word(@MovementTmp1, ,@MovementTmp2) @MovementTmp3}
      }
     };
     #If {@MovementTmp2=-1} {
      #If {@GagTreadLine=1} {#GagLine 1 All};
      MStep You Leave {@MovementDir}
     }
    }
   } {
    #Var MovementTmp2 {%Pos( in from ,@MovementTmp1 )};
    #If {@MovementTmp2>0} {
     #Var MovementTmp1 {%Copy(@MovementTmp1,1,@MovementTmp2-1)};
     #Var MovementTmp2 {%NumWords(@MovementTmp1, )};
     #Var MovementTmp3 {%Word(@MovementTmp1, ,@MovementTmp2)};
     #While {@MovementTmp2>1 and @MovementTmp4>0} {
      #Math MovementTmp4 {@MovementTmp4-1};
      #If {%IfWord(@MovementTmp3,@WalkStyles,%CRLF)=1} {
       #Var MovementTmp2 {-1}
      } {
       #Math MovementTmp2 {@MovementTmp2-1};
       #Var MovementTmp3 {%Word(@MovementTmp1, ,@MovementTmp2) @MovementTmp3}
      }
     };
     #If {@MovementTmp2=-1} {
      MStep Other Arrive {@MovementDir}
     }
    } {
     #Var MovementTmp2 {%NumWords(@MovementTmp1, )};
     #Var MovementTmp3 {%Word(@MovementTmp1, ,@MovementTmp2)};
     #While {@MovementTmp2>1 and @MovementTmp4>0} {
      #Math MovementTmp4 {@MovementTmp4-1};
      #If {%IfWord(@MovementTmp3,@WalkStyles,%CRLF)=1} {
       #Var MovementTmp2 {-1}
      } {
       #Math MovementTmp2 {@MovementTmp2-1};
       #Var MovementTmp3 {%Word(@MovementTmp1, ,@MovementTmp2) @MovementTmp3}
      }
     };
     #If {@MovementTmp2=-1} {
      #Var OtherDir {@MovementDir};
      #Var OtherName {%Copy(@MovementTmp1,1,%Length(@MovementTmp1)-%Length(@MovementTmp3)-1)};
      MStep Other Leave {@MovementDir}
     }
    }
   };
   #4 {#UnVar {MovementTmp%i}}
  }
 }
} {Main}

#Trigger {Please do not enter so many commands at once.} {msplay General\Misc\spam@Ext} {Main|SpamAlerts}
#Trigger {Spamming weapon commands will get you nowhere. Please be considerate. If you are experiencing lag, it would be best to wait for one command to successfully go through before attempting another. Thank you.} {msplay General\Misc\spam@Ext} {Main|SpamAlerts}
#Trigger {A* sanitation drone *.} {
 #If {%IfWord(%Word(%2, ,1),arrives collects goes whirls, )=1} {
  #If {%Word(%2, ,1)="arrives"} {mrandomplay General\Misc\DroneEnters*2@Ext};
  #If {%Word(%2, ,1)="collects"} {mrandomplay General\Misc\DroneCollects*2@Ext};
  #If {%Word(%2, ,1)="goes"} {mrandomplay General\Misc\DroneLeaves*2@Ext};
  #If {%Word(%2, ,1)="whirls"} {mrandomplay General\Misc\DroneWhirls*5@Ext}
 }
} {Main}
#Trigger {A* drone rushes into the room and begins recharging any nearby drones. Just as quickly as it enters, it vanishes.} {
 #Sub {Drones recharged.};
 msplay General\Misc\DroneRecharge@Ext
} {Main}
#Trigger {You seem to have left * behind!} {
 #Sub {You left %1 behind.};
 PushMessage Left {You left %1 behind.};
 msplay General\Misc\LeftPerson@Ext
} {Main}
#Trigger {Wait *} {
 #If {%IsNumber(%Word(%1, ,1))=1 and @PlayWhenThwarted=1} {msplay General\Misc\vollimit@Ext 20};
 #If {@Main<0} {MainTrigSwitch 1}
}

#Trigger {A bright ring of light emerges from the * and comes to a halt. * steps off.} {
 #Sub {%2 emerges from the %1.};
 msplay General\Misc\HGLift@Ext
} {Main}
#Trigger {* steps on to a small platform on the floor. Sensing * presence, a ring of lights along the perimeter of the circle light up as the platform shoots * out of sight.} {
 #Sub {%1 shoots %3.};
 msplay General\Misc\HGLift@Ext
} {Main}
#Trigger {*ancient pirate chest*here.} {msplay General\Archaeology\practice@Ext} {Main}
#Trigger {* ha* been loaded into your courier ship. Your destination is * in Sector *.} {
 Speech Stop;
 #Sub {%1 for %3 in %4.}
} {Main}
#Trigger {* pull* a lever on a* and *the reels start to spin!} {Msplay General\Misc\skeeballrolls@Ext} {Main}
#Trigger {Your droids and drones suddenly stop following you, choicelessly obeying an order programmed into them.} {msplay General\Misc\InstructDrone@Ext} {Main}
#Trigger {You ascend the stairs and push through a set of rotating glass doors.} {
 DontStopSpeaking;
 msplay General\Misc\Door~@Ext
} {Main}
#Trigger {You push through a set of rotating glass doors.} {
 DontStopSpeaking;
 msplay General\Misc\Door~@Ext
} {Main}

#Trigger {You push through a metal turnstile.} {DontStopSpeaking} {Main}
#Trigger {To the immediate * you see *} {#Sub {%1: %2}} {Main}
#Trigger {This *} {
 #Var ShipMiscTmp1 {%Word(%1, ,1)};
 #Var ShipMiscTmp1 {%Replace(@ShipMiscTmp1,~",)};
 #Var ShipMiscTmp1 {%Replace(@ShipMiscTmp1,~~,)};
 #If {%IfWord(@ShipMiscTmp1,moon planet ship station, )=1} {
  #Var ShipMiscTmp1 {%Word(%1, ,2)};
  #Var ShipMiscTmp1 {%Replace(@ShipMiscTmp1,~",)};
  #Var ShipMiscTmp1 {%Replace(@ShipMiscTmp1,~~,)};
  #Var ShipMiscTmp1 {%Replace(@ShipMiscTmp1,~,,)};
  #If {%IfWord(@ShipMiscTmp1,demands transmits surrenders, )=1} {
   msplay Ship\Comm\Sector@Ext
  };
  #If {%IfWord(@ShipMiscTmp1,broadcasts, )=1} {
   msplay Ship\Comm\Broadcast@Ext
  }
 }
} {Main}
#Trigger {* sit* down on *} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0} {
  msplay General\Misc\sit@Ext;
  PushMessage RP {%0}
 }
} {Main}
#Trigger {* stand* up.} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0} {
  msplay General\Misc\stand@Ext;
  PushMessage RP {%0}
 }
} {Main}
#Trigger {* pull* to * feet.} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0} {
  msplay General\Misc\stand@Ext;
  PushMessage RP {%0}
 }
} {Main}
#Trigger {* yawns suddenly and collapses to the ground, asleep.} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0} {
  msplay {General\Socials\yawn*2@Ext};
  msplay {General\Socials\collapse*3@Ext}
 }
} {Main}

#Trigger {You * * lockbox*.} {
 #If {%IfWord(%1,open close, )=1} {
  msplay general\misc\lockbox%1@Ext
 }
} {Main}

#Trigger {With a gentle jolt, the ferry*} {
 msplay general\misc\ferry@Ext;
 #If {@SoundIsPlaying=1} {#Var FerryHandle %PlayHandle}
} {Main}
#Trigger {You barely notice as the ferry makes contact with the dock and is securely tied in place*} {
 msplay general\misc\ferrystop@Ext;
 #If {@SoundIsPlaying=1} {#Var FerryHandle %PlayHandle}
} {Main}
#Trigger {You watch as a ferry arrives and gets secured to the dock.} {
 msplay general\misc\ferrystop@Ext
} {Main}
#Trigger {You disembark.} {
 #If {%Defined(FerryHandle)=1} {
  #PC @FerryHandle stop;
  #UnVar FerryHandle
 } {
  msplay General\Misc\Disembark@Ext
 }
} {Main}
#Trigger {You follow * to * Large Wooden Dock.} {
 #Var Following 1;
 #If {%Defined(FerryHandle)=1} {
  #PC @FerryHandle stop;
  #UnVar FerryHandle
 }
} {Main}
#Trigger {* unclip* from *.} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0} {
  msplay General\Misc\Belt1@Ext
 }
} {Main}
#Trigger {* clip* to *.} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0 and %NumWords(%1, )<5} {
  msplay General\Misc\Belt2@Ext
 }
} {Main}
#Trigger {* knock* on the *.} {
 #If {%Pos(~, seeming vaguely puzzled,%0)=0 and %Pos(:,%0)=0 and %IfWord(%Left(%0,1),>| |[|~(|-,|)=0 and %IfWord(%3,hull,|)=0} {
  msplay General\Misc\Knock1@Ext
 }
} {Main}
#Trigger {* impatiently knock* on the *.} {msplay General\Misc\Knock2@Ext} {Main}
#Trigger {* kick* furiously on the *.} {msplay General\Misc\Knock3@Ext} {Main}
#Trigger {You * credit*} {
 #If {%Defined(RewardAlarmHandle)=0 and %IfWord(%Word(%1, ,1),earn receive, )=1} {
  #Alarm RewardAlarmHandle 1 {#UnVar RewardAlarmHandle};
  MirianiSoundPlay Reward {General\Misc\Credits%If(%Pos(earn,%1)=1,4@Ext,*3@Ext)}
 }
} {Main}
#Trigger {You are seated in a chair with several syringes on robotic arms around it.  *} {msplay General\Misc\MedicalOpperation@Ext} {Main}

#Trigger {A large planetary mining drone beeps quietly*} {
 msplay General\PMining\store@Ext;
 #GagLine 1 All
} {Main|Ground}
#Trigger {A recorded voice on a large planetary mining drone whispers, "A pocket of valuable minerals has been detected." You set it on the ground and *} {
 msplay General\PMining\detected@Ext;
 #Sub {Starting dig.}
} {Main|Ground}
#Trigger {The transport, wasting no time, lifts off and*} {msplay General\PMining\Transport@Ext} {Main|Ground}
#Trigger {With a barely noticeable jolt, the mining transport *} {msplay General\PMining\TRExit@Ext} {Main|Ground}
#Trigger {A large planetary mining drone} {
 #GagLine 1 all
} {Main|Ground}
#Trigger {There is a large number * stenciled on the side of the drone.} {
 #Sub {Drone %1.}
} {Main|Ground}
#Trigger {A small screen indicates that it has stored * out of 20 minerals. It *.} {
 #Sub {%1 of 20. %If(%Pos(digging,%2)>0,Active,Inactive).}
} {Main|Ground}
#Trigger {A* air freshener freshens the room with the scent of *.} {msplay Ship\Misc\Scent@Ext} {Main}

#Alias rem {#Exec {remove%RTrim( %0)}}
#Alias wa {#Exec {walk%RTrim( %0)}}
